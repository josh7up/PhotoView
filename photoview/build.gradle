apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion rootProject.ext.sdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion rootProject.ext.sdkVersion
        versionCode 1
        versionName "1.0"
    }
}

dependencies {
    compile "com.android.support:support-core-utils:$supportLibVersion"
}

apply from: 'https://raw.githubusercontent.com/Commit451/gradle-android-javadocs/1.0.0/gradle-android-javadocs.gradle'

def myGroupId = 'com.github.josh7up'
def myArtifactId = 'PhotoView'
def myVersionNumber = '2.1.5'
def myUsername = 'josh7up'
def myGitUrl = 'https://github.com/josh7up/PhotoView'

group = myGroupId
version = myVersionNumber
// Important for ensuring this library project is not uploaded to bintray as a sub-project.
archivesBaseName = myArtifactId

install {
    repositories.mavenInstaller {
        pom.project {
            name myArtifactId
            description 'Implementation of ImageView for Android that supports zooming, by various touch gestures.'
            url myGitUrl
            inceptionYear '2017'

            packaging 'aar'
            groupId myGroupId
            artifactId myArtifactId
            version myVersionNumber

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection "${myGitUrl}.git"
                url myGitUrl

            }
            developers {
                developer {
                    id = myUsername
                    name 'Josh'
                }
            }
        }
    }
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = bintrayProperties.getProperty('user')
    key = bintrayProperties.get('key')
    configurations = ['archives']
    pkg {
        repo = 'android'
        name = myArtifactId
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
        publish = true
        version {
            name = myVersionNumber
            desc = "${myArtifactId} ${myVersionNumber}"
            released = new Date()
            vcsTag = myVersionNumber
        }
    }
}